import java.util.Properties

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def googleMapsApiKey = localProperties.getProperty('GOOGLE_MAPS_API_KEY') ?: ""

android {
    namespace 'com.aircare'
    compileSdk 34

    defaultConfig {
        applicationId 'com.aircare'
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName '1.0'

        buildConfigField 'String', 'GOOGLE_MAPS_API_KEY', "\"${googleMapsApiKey}\""
        resValue 'string', 'google_maps_api_key', googleMapsApiKey
        manifestPlaceholders = [GOOGLE_MAPS_API_KEY: googleMapsApiKey]
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    implementation "com.google.android.gms:play-services-maps:19.2.0"
    implementation "com.google.android.libraries.places:places:4.4.1"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "com.google.code.gson:gson:2.11.0"
}
